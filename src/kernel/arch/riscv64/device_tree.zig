const std = @import("std");
const kernel = @import("root");
const assert = kernel.assert;
const TODO = kernel.TODO;
const align_forward = kernel.align_forward;
const string_eq = kernel.string_eq;
const starts_with = kernel.string_starts_with;
const ends_with = kernel.string_ends_with;
const read_big_endian = std.mem.readIntSliceBig;
const page_size = kernel.arch.page_size;
const Memory = kernel.Memory;

const print = kernel.arch.print;
const write = kernel.arch.write;

const DeviceTree = @This();
var result: DeviceTree.Result = undefined;
const soft_separator = "----------------------------------------------------------------\n";
const hard_separator = "================================================================\n";

header: Header,
base_address: u64,
main_nodes_start: u64,

pub fn parse(self: *@This()) void {
    write(hard_separator);
    defer write(hard_separator);
    print("Starting parsing the Flattened Device Tree...\n", .{});
    self.header = DeviceTree.Header.read(@intToPtr([*]const u8, self.base_address)[0..@sizeOf(DeviceTree.Header)]) catch unreachable;
    DeviceTree.MemoryReservationBlock.parse(self.header, self.base_address);
    var dt_structure_block_parser = DeviceTree.StructureBlock.Parser{ .slice = undefined, .i = 0, .device_tree = self };
    dt_structure_block_parser.parse();
    print("Done parsing the FDT\n", .{});
}

pub const SearchType = enum {
    exact,
    start,
    end,
};

pub fn get_node_finding_parser(self: *@This()) StructureBlock.Parser {
    const slice_size = self.header.device_tree_struct_size - self.main_nodes_start;
    return StructureBlock.Parser{
        .slice = @intToPtr([*]u8, self.base_address + self.header.device_tree_struct_offset + self.main_nodes_start)[0..slice_size],
        .i = 0,
        .device_tree = self,
    };
}

pub fn find_property(self: *@This(), main_node: []const u8, property_name: []const u8, comptime search_type: SearchType, maybe_intermediate_nodes: ?[]const []const u8, comptime maybe_intermediate_search_types: ?[]const SearchType) ?StructureBlock.Parser.Property {
    var parser = self.get_node_finding_parser();

    if (parser.find_node_from_current_offset(main_node, search_type)) |_| {
        if (maybe_intermediate_nodes) |intermediate_nodes| {
            if (maybe_intermediate_search_types) |intermediate_search_types| {
                var last_node = false;
                for (intermediate_nodes) |node, i| {
                    const intermediate_search_type = intermediate_search_types[i];
                    last_node = parser.find_node_from_current_offset(node, intermediate_search_type) != null;
                }
                kernel.assert(@src(), last_node);
                return parser.find_property_in_current_node(property_name);
            }
        } else {
            return parser.find_property_in_current_node(property_name);
        }
    }

    return null;
}

const FindNodeResult = struct {
    parser: StructureBlock.Parser,
    name: []const u8,
};

pub fn find_node(self: *@This(), node: []const u8, comptime search_type: SearchType) ?FindNodeResult {
    var parser = self.get_node_finding_parser();
    if (parser.find_node_from_current_offset(node, search_type)) |node_name| {
        return FindNodeResult{
            .parser = parser,
            .name = node_name,
        };
    }

    return null;
}

const Header = struct {
    magic: u32,
    size: u32,
    device_tree_struct_offset: u32,
    device_tree_strings_offset: u32,
    rsvmap_memory_offset: u32,
    version: u32,
    last_comp_version: u32,
    boot_cpuid_physical_address: u32,
    device_tree_strings_size: u32,
    device_tree_struct_size: u32,

    const expected_magic = 0xd00dfeed;

    const ReadError = error{
        incorrect_magic,
    };

    fn read(bytes: []const u8) ReadError!DeviceTree.Header {
        var bytes_it = bytes;
        var device_tree_header: DeviceTree.Header = undefined;
        var device_tree_it_bytes = @ptrCast([*]u32, &device_tree_header);

        for (device_tree_it_bytes[0 .. @sizeOf(DeviceTree.Header) / @sizeOf(u32)]) |*device_tree_n| {
            device_tree_n.* = read_big_endian(u32, bytes_it);
            bytes_it.ptr += @sizeOf(u32);
            bytes_it.len -= @sizeOf(u32);
        }

        assert(@src(), device_tree_header.magic == expected_magic);

        return device_tree_header;
    }
};

const MemoryReservationBlock = struct {
    const Entry = struct {
        address: u64,
        size: u64,
    };

    fn parse(header: Header, header_offset: u64) void {
        const memory_reservation_block_offset = header.rsvmap_memory_offset;
        const block_address = header_offset + memory_reservation_block_offset;
        var block_it = @intToPtr([*]u8, block_address);

        var entry_count: u64 = 0;

        while (true) {
            var entry: Entry = undefined;
            entry.address = read_big_endian(u64, block_it[0..@sizeOf(u64)]);
            block_it += @sizeOf(u64);
            entry.size = read_big_endian(u64, block_it[0..@sizeOf(u64)]);
            block_it += @sizeOf(u64);

            if (entry.address == 0 and entry.size == 0) break;
            print("Entry (0x{x}, 0x{x})\n", .{ entry.address, entry.size });
            entry_count += 1;
        }
    }
};

const StructureBlock = struct {
    const Parser = struct {
        slice: []const u8,
        i: u64,
        device_tree: *DeviceTree,

        fn parse(self: *@This()) void {
            const offset = self.device_tree.header.device_tree_struct_offset;
            const size = self.device_tree.header.device_tree_struct_size;
            const address = self.device_tree.base_address + offset;

            self.slice = @intToPtr([*]u8, address)[0..size];

            var address_cells: u32 = 0;
            var size_cells: u32 = 0;

            while (true) {
                const main_token: Token = self.parse_token();
                if (main_token == .end) break;
                assert(@src(), self.i < self.slice.len);
                assert(@src(), main_token == .begin_node);

                const node_name = self.parse_begin_node();
                assert(@src(), string_eq(node_name, ""));

                while (true) {
                    const token = self.parse_token();

                    switch (token) {
                        .property => {
                            const descriptor = self.parse_property_value_descriptor();
                            const key = self.parse_string_in_string_table(descriptor);
                            print("{s}: ", .{key});

                            if (string_eq(key, "#address-cells")) {
                                assert(@src(), descriptor.len == @sizeOf(u32));
                                address_cells = self.parse_int(u32);
                                print("{}\n", .{address_cells});
                            } else if (string_eq(key, "#size-cells")) {
                                assert(@src(), descriptor.len == @sizeOf(u32));
                                size_cells = self.parse_int(u32);
                                print("{}\n", .{size_cells});
                            } else if (string_eq(key, "compatible")) {
                                const value = self.parse_property_name(descriptor);
                                print("{s}\n", .{value});
                            } else if (string_eq(key, "model")) {
                                const value = self.parse_property_name(descriptor);
                                print("{s}\n", .{value});
                            } else {
                                TODO(@src());
                            }
                        },
                        .begin_node => {
                            if (self.device_tree.main_nodes_start == 0) {
                                self.device_tree.main_nodes_start = self.i - @sizeOf(Token);
                            }
                            self.parse_node(1, address_cells, size_cells);
                        },
                        .end_node => break,
                        else => kernel.crash("Unexpected token: {}\n", .{token}),
                    }
                }
            }

            // Add the kernel memory region
            //const kernel_address = kernel.bounds.get_start();
            //const kernel_end = kernel.bounds.get_end();
            //const kernel_size = kernel_end - kernel_address;
            //assert(@src(), kernel_address & (page_size - 1) == 0);
            //assert(@src(), kernel_end & (page_size - 1) == 0);
            //result.reserved_memory_regions[result.reserved_memory_region_count].address = kernel_address;
            //result.reserved_memory_regions[result.reserved_memory_region_count].size = kernel_size;
            //result.reserved_memory_region_count += 1;

            //// Add the FDT memory region
            //result.reserved_memory_regions[result.reserved_memory_region_count].address = header_address;
            //result.reserved_memory_regions[result.reserved_memory_region_count].size = align_forward(header.size, page_size);
            //result.reserved_memory_region_count += 1;

            //result.address = header_address;

            //return &result;
        }

        fn parse_node(self: *@This(), identation: u32, parent_address_cells: u32, parent_size_cells: u32) void {
            const node_name = self.parse_begin_node();
            print_ident(identation, "* {s}:\n", .{node_name});

            const attribute_identation = identation + 1;
            _ = attribute_identation;

            var address_cells: u32 = parent_address_cells;
            var size_cells: u32 = parent_size_cells;

            while (true) {
                const token = self.parse_token();

                switch (token) {
                    .property => {
                        const property_value_descriptor = self.parse_property_value_descriptor();
                        const property_name = self.parse_string_in_string_table(property_value_descriptor);

                        print_ident(attribute_identation, "{s}: ", .{property_name});
                        // First check the standard ones
                        if (string_eq(property_name, "compatible")) {
                            const value = self.parse_property_name(property_value_descriptor);
                            print("{s}", .{value});
                        } else if (string_eq(property_name, "model")) {
                            TODO(@src());
                        } else if (string_eq(property_name, "phandle")) {
                            assert(@src(), property_value_descriptor.len == @sizeOf(u32));
                            const value = self.parse_int(u32);
                            print("{}", .{value});
                        } else if (string_eq(property_name, "status")) {
                            const value = self.parse_property_name(property_value_descriptor);
                            print("{s}", .{value});
                        } else if (string_eq(property_name, "#address-cells")) {
                            assert(@src(), property_value_descriptor.len == @sizeOf(u32));
                            address_cells = self.parse_int(u32);
                            print("{}", .{address_cells});
                        } else if (string_eq(property_name, "#size-cells")) {
                            assert(@src(), property_value_descriptor.len == @sizeOf(u32));
                            size_cells = self.parse_int(u32);
                            print_ident(attribute_identation, "{}", .{size_cells});
                        } else if (string_eq(property_name, "reg")) {
                            const address_byte_count = address_cells * @sizeOf(u32);
                            const size_byte_count = size_cells * @sizeOf(u32);
                            const pair_byte_count = address_byte_count + size_byte_count;
                            const pair_count = property_value_descriptor.len / pair_byte_count;
                            var pair_i: u64 = 0;
                            while (pair_i < pair_count) : (pair_i += 1) {
                                write("(");

                                switch (address_byte_count) {
                                    @sizeOf(u32) => {
                                        const value = self.parse_int(u32);
                                        print("0x{x}", .{value});
                                    },
                                    @sizeOf(u64) => {
                                        const value = self.parse_int(u64);
                                        print("0x{x}", .{value});
                                    },
                                    0 => {},
                                    else => unreachable,
                                }

                                write(" , ");

                                switch (size_byte_count) {
                                    @sizeOf(u64) => {
                                        const value = self.parse_int(u64);
                                        print("0x{x}", .{value});
                                    },
                                    0 => {},
                                    else => unreachable,
                                }

                                write("), ");
                            }
                        } else if (string_eq(property_name, "virtual-reg")) {
                            TODO(@src());
                        } else if (string_eq(property_name, "ranges")) {
                            if (property_value_descriptor.len != 0) {
                                self.i += property_value_descriptor.len;
                                write("TODO");
                                //write("\n");
                                //for (self.slice[i .. i + 100]) |b, index| {
                                //print("[{}] {c}\n", .{ index, b });
                                //}
                                //TODO(@src());
                            } else {
                                write("empty");
                                //self.i += 1;
                            }
                        } else if (string_eq(property_name, "dma-ranges")) {
                            TODO(@src());
                        } else if (string_eq(property_name, "dma-coherent")) {
                            assert(@src(), property_value_descriptor.len == 0);
                        } else {
                            //Non-standard ones
                            if (starts_with(node_name, "flash")) {
                                if (string_eq(property_name, "bank-width")) {
                                    assert(@src(), property_value_descriptor.len == @sizeOf(u32));
                                    const bank_width = self.parse_int(u32);
                                    print("{}", .{bank_width});
                                } else {
                                    TODO(@src());
                                }
                            } else if (string_eq(node_name, "chosen")) {
                                // Chosen is a standard node
                                if (string_eq(property_name, "bootargs")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else if (string_eq(property_name, "stdout-path")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else {
                                    print("Property unknown: {s}", .{property_name});
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "memory")) {
                                if (string_eq(property_name, "device_type")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (string_eq(node_name, "cpus")) {
                                if (string_eq(property_name, "timebase-frequency")) {
                                    self.parse_and_print_freq(property_value_descriptor);
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "cpu@")) {
                                if (string_eq(property_name, "device_type")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else if (string_eq(property_name, "riscv,isa")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else if (string_eq(property_name, "mmu-type")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (string_eq(node_name, "interrupt-controller")) {
                                if (string_eq(property_name, "#interrupt-cells")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "interrupt-controller")) {
                                    assert(@src(), property_value_descriptor.len == 0);
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "core")) {
                                if (string_eq(property_name, "cpu")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "rtc@") or starts_with(node_name, "uart@")) {
                                if (string_eq(property_name, "interrupts")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "interrupt-parent")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                    //const value = property_value_descriptor.len;
                                    //print("{}", .{value});
                                    //TODO(@src());
                                } else if (string_eq(property_name, "clock-frequency")) {
                                    self.parse_and_print_freq(property_value_descriptor);
                                } else {
                                    TODO(@src());
                                }
                            } else if (string_eq(node_name, "poweroff") or string_eq(node_name, "reboot")) {
                                if (string_eq(property_name, "value")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "offset")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "regmap")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "pci@")) {
                                if (string_eq(property_name, "interrupt-map-mask")) {
                                    const value = self.parse_int(u64);
                                    const value2 = self.parse_int(u64);
                                    print("0x{x}, 0x{x}", .{ value, value2 });
                                } else if (string_eq(property_name, "interrupt-map")) {
                                    // TODO
                                    self.i += property_value_descriptor.len;
                                    //var i: u32 = 0;
                                    //while (i < byte_count) : (i += @sizeOf(u64)) {
                                    //_ = self.parse_int(u64);
                                    //}
                                    write("TODO");
                                } else if (string_eq(property_name, "bus-range")) {
                                    const value = self.parse_int(u64);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "linux,pci-domain")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "device_type")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else if (string_eq(property_name, "#interrupt-cells")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "virtio_mmio@")) {
                                if (string_eq(property_name, "interrupts")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "interrupt-parent")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "plic@")) {
                                if (string_eq(property_name, "riscv,ndev")) {
                                    const value = self.parse_int(u32);
                                    print("{}", .{value});
                                } else if (string_eq(property_name, "interrupts-extended")) {
                                    //const value = property_value_descriptor.len;
                                    const value1 = self.parse_int(u32);
                                    const value2 = self.parse_int(u32);
                                    const value3 = self.parse_int(u32);
                                    const value4 = self.parse_int(u32);
                                    print("{}, {}, {}, {} ", .{ value1, value2, value3, value4 });
                                    write("TODO");
                                } else if (string_eq(property_name, "interrupt-controller")) {
                                    assert(@src(), property_value_descriptor.len == 0);
                                } else if (string_eq(property_name, "#interrupt-cells")) {
                                    const value = self.parse_property_name(property_value_descriptor);
                                    print("{s}", .{value});
                                } else {
                                    TODO(@src());
                                }
                            } else if (starts_with(node_name, "clint@")) {
                                if (string_eq(property_name, "interrupts-extended")) {
                                    //const value = property_value_descriptor.len;
                                    const value1 = self.parse_int(u32);
                                    const value2 = self.parse_int(u32);
                                    const value3 = self.parse_int(u32);
                                    const value4 = self.parse_int(u32);
                                    print("{}, {}, {}, {} ", .{ value1, value2, value3, value4 });
                                    write("TODO");
                                } else {
                                    TODO(@src());
                                }
                            } else {
                                TODO(@src());
                            }
                        }

                        write("\n");
                    },
                    .begin_node => self.parse_node(attribute_identation + 1, address_cells, size_cells),
                    .end_node => break,
                    else => kernel.crash("NI: {s}\n", .{@tagName(token)}),
                }
            }
        }

        // This assumes the begin_node token has already been parsed
        pub fn find_property_in_current_node(self: *@This(), wanted_property_name: []const u8) ?Property {
            while (true) {
                const token = self.parse_token();
                switch (token) {
                    .property => {
                        const property_value_descriptor = self.parse_property_value_descriptor();
                        const property_name = self.parse_string_in_string_table(property_value_descriptor);
                        const property_value = self.slice.ptr[self.i .. self.i + property_value_descriptor.len];
                        self.i = align_to_u32(self.i + property_value_descriptor.len);

                        if (string_eq(property_name, wanted_property_name)) {
                            return Property{
                                .name = property_name,
                                .value = property_value,
                            };
                        }
                    },
                    else => kernel.crash("NI find: {}\n", .{token}),
                }
            }

            return null;
        }

        fn skip_property_value(self: *@This(), property_value_descriptor: Property.ValueDescriptor) void {
            self.i = align_to_u32(self.i + property_value_descriptor.len);
        }

        fn parse_and_print_freq(self: *@This(), property_value_descriptor: Property.ValueDescriptor) void {
            switch (property_value_descriptor.len) {
                @sizeOf(u32) => {
                    const value = self.parse_int(u32);
                    print("{} Hz", .{value});
                },
                @sizeOf(u64) => {
                    TODO(@src());
                },
                else => unreachable,
            }
        }

        fn print_ident(identation: u32, comptime format: []const u8, args: anytype) void {
            var ident_it: u32 = 0;
            while (ident_it < identation) : (ident_it += 1) {
                write("    ");
            }

            print(format, args);
        }

        fn write_ident(identation: u32, bytes: []const u8) void {
            var ident_it: u32 = 0;
            while (ident_it < identation) : (ident_it += 1) {
                write("    ");
            }
            write(bytes);
        }

        fn parse_property_name(self: *@This(), descriptor: Property.ValueDescriptor) []const u8 {
            const property_value = self.slice[self.i .. self.i + descriptor.len];
            self.i = align_to_u32(self.i + descriptor.len);
            return property_value;
        }

        //fn parse_properly_encoded_array(self: *@This(), descriptor: Property.ValueDescriptor) []const u8 {
        //}

        pub fn skip_node(self: *@This()) void {
            while (true) {
                const skip_token = self.parse_token();
                switch (skip_token) {
                    .begin_node => {
                        self.skip_cstr();
                        self.skip_node();
                    },
                    .property => {
                        self.skip_property();
                    },
                    .end_node => {
                        break;
                    },
                    else => kernel.crash("token unimplemented: {}\n", .{skip_token}),
                }
            }
        }

        fn skip_property(self: *@This()) void {
            const descriptor = self.parse_property_value_descriptor();
            self.skip_property_value(descriptor);
        }

        fn skip_cstr(self: *@This()) void {
            const len = std.mem.len(@ptrCast([*:0]const u8, self.slice[self.i..].ptr));
            self.i = align_to_u32(self.i + len + 1);
        }

        fn parse_begin_node(self: *@This()) []const u8 {
            const node_name = self.read_cstr_advancing_it();
            self.i = align_to_u32(self.i);
            return node_name;
        }

        fn parse_property_value_descriptor(self: *@This()) Property.ValueDescriptor {
            return Property.ValueDescriptor{
                .len = self.parse_int(u32),
                .name_offset = self.parse_int(u32),
            };
        }

        fn parse_string_in_string_table(self: *@This(), descriptor: Property.ValueDescriptor) []const u8 {
            const strings_offset = self.device_tree.header.device_tree_strings_offset;
            const string_offset = self.device_tree.base_address + strings_offset + descriptor.name_offset;
            const property_key_cstr = @intToPtr([*:0]u8, string_offset);
            const value = property_key_cstr[0..std.mem.len(property_key_cstr)];
            return value;
        }

        fn read_cstr_advancing_it(self: *@This()) []const u8 {
            const cstr_len = std.mem.len(@ptrCast([*:0]const u8, self.slice[self.i..].ptr));
            const cstr = self.slice[self.i .. self.i + cstr_len];
            self.i += cstr_len + 1;
            return cstr;
        }

        fn parse_token(self: *@This()) Token {
            assert(@src(), self.i & 0b11 == 0);
            const token_int = self.parse_int(u32);
            //logger.debug("Trying to cast possible valid token {} into an enum\n", .{token_int});
            const token = @intToEnum(Token, token_int);
            return token;
        }

        fn parse_int(self: *@This(), comptime Int: type) Int {
            const int = read_big_endian(Int, self.slice[self.i..]);
            self.i += @sizeOf(Int);
            return int;
        }

        const Token = enum(u32) {
            begin_node = 1,
            end_node = 2,
            property = 3,
            nop = 4,
            end = 9,
        };

        const Property = struct {
            name: []const u8,
            value: []const u8,

            const ValueDescriptor = struct {
                len: u32,
                name_offset: u32,
            };
        };

        const Types = enum(u32) {
            empty = 0,
            int32 = 1,
            int64 = 2,
            string = 3,
            phandle = 4,
            string_list = 5,
        };

        inline fn align_to_u32(i: u64) u64 {
            return align_forward(i, @sizeOf(u32));
        }

        fn find_node_from_current_offset(self: *@This(), wanted_node_name: []const u8, search_type: SearchType) ?[]const u8 {
            while (true) {
                const token = self.parse_token();
                switch (token) {
                    .begin_node => {
                        const node_name = self.parse_begin_node();

                        const found = switch (search_type) {
                            .exact => string_eq(node_name, wanted_node_name),
                            .start => starts_with(node_name, wanted_node_name),
                            .end => ends_with(node_name, wanted_node_name),
                        };

                        if (found) {
                            return node_name;
                        }

                        self.skip_node();
                    },
                    .property => self.skip_property(),
                    .end_node => break,
                    else => kernel.crash("NI: {}\n", .{token}),
                }
            }

            return null;
        }

        pub fn get_subnode(self: *@This()) ?[]const u8 {
            while (true) {
                const token = self.parse_token();
                switch (token) {
                    .begin_node => {
                        const node_name = self.parse_begin_node();
                        return node_name;
                    },
                    .property => self.skip_property(),
                    .end_node => break,
                    else => unreachable,
                }
            }

            return null;
        }
    };
};

pub const Result = struct {
    memory_regions: [1024]Memory.Region.Descriptor,
    memory_region_count: u64,
    reserved_memory_regions: [64]Memory.Region.Descriptor,
    reserved_memory_region_count: u64,
    address: u64,
};
